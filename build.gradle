plugins {
    id "java"
    id "application"
}

group "net.mojodna.osm2orc"
version "0.6.1"

application {
    mainClass = "net.mojodna.osm2orc.Osm2Orc"
}

distributions {
    main {
        contents {
            // omit dependenciess (they're included in the aseembly) by
            // targeting our JAR + generated wrapper scripts
            include "**/osm2orc*"
        }
    }
}

dependencies {
    compileOnly group: "org.openstreetmap.osmosis", name: "osmosis-core", version: "0.49.1"
    compileOnly group: "org.openstreetmap.osmosis", name: "osmosis-xml", version: "0.49.1"

    implementation group: "de.topobyte", name: "osm4j-pbf", version: "1.3.0"
    implementation group: "de.topobyte", name: "osm4j-core", version: "1.3.0"
    implementation group: "org.apache.orc", name: "orc-core", version: "1.8.4"
    implementation group: "org.apache.hadoop", name: "hadoop-common", version: "3.3.6"

    runtimeOnly group: "org.apache.hadoop", name: "hadoop-aws", version: "3.3.6"
    runtimeOnly group: "org.apache.hadoop", name: "hadoop-hdfs-client", version: "3.3.6"
}

jar {
    manifest {
        attributes(
                "Main-Class": "net.mojodna.osm2orc.Osm2Orc"
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    archiveClassifier = "all"
    duplicatesStrategy "exclude"
    zip64 true
}

repositories {
    mavenCentral()
    maven {
        url "https://mvn.topobyte.de"
    }
    maven {
        url "https://mvn.slimjars.com"
    }
}

